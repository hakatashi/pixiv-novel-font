#!/bin/env fontforge

ipam = "fonts/ipam.ttf"
ipamp = "fonts/ipamp.ttf"
output = "pixiv-novel-font.ttf"

copyright = "Copyright(c) Information-technology Promotion Agency, Japan (IPA), 2003-2011. Copyright(c) Koki Takahashi, 1015. You must accept \"http://ipafont.ipa.go.jp/ipa_font_license_v1.html\" to use this product."
fontname_en = "PixivNovelFont"
fontname_ja = "PixivNovelFont"
version = "0.0.0"
website = "https://github.com/hakatashi/pixiv-novel-font"

Open(ipam)

# Set default font name
SetFontNames(fontname_en, fontname_en, fontname_en)

# Set general font information
SetTTFName(0x0, 0, copyright)
SetTTFName(0x0, 1, fontname_en)
SetTTFName(0x0, 3, fontname_en + " " + version)
SetTTFName(0x0, 4, fontname_en)
SetTTFName(0x0, 5, version)
SetTTFName(0x0, 13, website)
SetTTFName(0x0, 14, website)

# Set English font information
SetTTFName(0x409, 0, copyright)
SetTTFName(0x409, 1, fontname_en)
SetTTFName(0x409, 3, fontname_en + " " + version)
SetTTFName(0x409, 4, fontname_en)
SetTTFName(0x409, 5, version)
SetTTFName(0x409, 13, website)
SetTTFName(0x409, 14, website)

# Set Japanese font information
SetTTFName(0x411, 0, copyright)
SetTTFName(0x411, 1, fontname_ja)
SetTTFName(0x411, 3, fontname_en + " " + version)
SetTTFName(0x411, 4, fontname_ja)
SetTTFName(0x411, 5, version)
SetTTFName(0x411, 13, website)
SetTTFName(0x411, 14, website)

# Merge IPAMinchoP
##################

# Select Japanese characters
SelectMore(0u3000, 0u30ff) # CJK Symbols, Hiragana and Katakana
SelectMore(0u3200, 0u9fff) # CJK Ideographs
SelectMore(0uf900, 0ufaff) # CJK Compatibility Ideographs
SelectMore(0ufe30, 0ufe4f) # CJK Compatibility Forms
SelectMore(0u20000, 0u2ffff) # CJK Ideographs Ext-B and more
# Exclude Space, for MergeFonts() doesn't import empty glyph from another font
SelectMore(0u20)

# Invert selection
SelectInvert()

# Remove glyphs
DetachAndRemoveGlyphs()

# Merge ipamp font
MergeFonts(ipamp)

# Replace Glyphs
################

# Vertical dash
Select("aj138.pw.vert")
Import("glyphs/dash-vert.svg", 16 & 2)
SetVWidth(2048)

# Horizontal dash
Select(0u2015)
Import("glyphs/dash.svg", 16 & 2)
SetWidth(2048)

# Clean
RemoveDetachedGlyphs()

Generate(output)
